plugins {
    id 'java'
    id 'io.github.patrick.remapper' version '1.4.2' apply false
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'io.github.patrick.remapper'
    def latestVersion = "1.21.4"
    def minecraftVersion

    if (project.name.startsWith("1")) {
        minecraftVersion = project.name
    } else if (project.name.contains("to_")) {
        def parts = project.name.split("to_")
        minecraftVersion = parts[1]
    } else if (project.name.contains("from_")) {
        def parts = project.name.split("from_")
        minecraftVersion = parts[1]
    } else {
        minecraftVersion = latestVersion
    }


    repositories {
        mavenCentral()
        mavenLocal()
        maven { url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
        maven { url = 'https://oss.sonatype.org/content/groups/public/' }
    }

    dependencies {
        if (project.name != "shared") {
            implementation(project(":nms:shared"))
            if (project.name != "api") {
                implementation(project(":nms:api"))
            }
        }
        compileOnly "org.spigotmc:spigot:$minecraftVersion-R0.1-SNAPSHOT:remapped-mojang"
    }

    tasks {
        remap {
            setVersion(minecraftVersion)
        }
    }

    build {
        dependsOn tasks.remap
    }
}
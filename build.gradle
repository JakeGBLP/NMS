import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'java'
    id 'com.gradleup.shadow' version '8.3.3'
    id 'io.github.patrick.remapper' version '1.4.2'
}

group = 'it.jakegblp'
version = '1.0.0'

dependencies {
    implementation project(":common")
    implementation project(":factory")
    implementation "dev.jorel:commandapi-bukkit-shade:9.7.0"
    annotationProcessor "dev.jorel:commandapi-annotations:9.7.0"
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'io.github.patrick.remapper'

    def isNmsModule = true
    def minecraftVersion

    if (name.startsWith("1")) {
        minecraftVersion = name
    } else if (name.contains("to_")) {
        def parts = name.split("to_")
        minecraftVersion = parts[1]
    } else if (name.contains("from_")) {
        def parts = name.split("from_")
        minecraftVersion = parts[1]
    } else {
        minecraftVersion = "1.21.7"
        isNmsModule = false
    }

    def javaVersion = 21

    java {
        toolchain.languageVersion.set(JavaLanguageVersion.of(javaVersion))
    }

    tasks.withType(JavaCompile).configureEach {
        sourceCompatibility = javaVersion
        targetCompatibility = javaVersion
    }

    repositories {
        mavenCentral()
        mavenLocal()
        maven { url = "https://jitpack.io/" }
        maven { url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
        maven { url = 'https://oss.sonatype.org/content/groups/public/' }
        maven { url = "https://repo.destroystokyo.com/repository/maven-public/" }
    }

    dependencies {
        compileOnly "org.spigotmc:spigot:$minecraftVersion-R0.1-SNAPSHOT:remapped-mojang"

        if (name == "factory" || name == "NMS" || isNmsModule) {
            implementation project(":common")
        }

        if (name == "factory") {
            rootProject.subprojects.each {
                if (it.path.startsWith(":nms:")) {
                    implementation project(it.path)
                }
            }
        }

        compileOnly 'it.unimi.dsi:fastutil:8.5.15'
        implementation "net.kyori:adventure-platform-bukkit:4.4.0"
        implementation "net.kyori:adventure-text-minimessage:4.23.0"
        implementation 'com.github.zafarkhaja:java-semver:0.10.2'
        implementation("org.jspecify:jspecify:1.0.0")
        compileOnly 'org.jetbrains:annotations:20.1.0'

        compileOnly 'org.projectlombok:lombok:1.18.36'
        annotationProcessor 'org.projectlombok:lombok:1.18.36'
        testCompileOnly 'org.projectlombok:lombok:1.18.36'
        testAnnotationProcessor 'org.projectlombok:lombok:1.18.36'
    }

    tasks {
        remap {
            setVersion(minecraftVersion)
        }
    }

    shadowJar {
        archiveClassifier.set("")
    }

    build {
        dependsOn shadowJar, tasks.remap
    }
}


processResources {
    filteringCharset = 'UTF-8'
    filter(ReplaceTokens, tokens: [version: version])
}

shadowJar {
    relocate("dev.jorel.commandapi", "it.jakegblp.nms.libraries.commandapi")
}

build {
    doLast {
        subprojects.each { subproject ->
            subproject.tasks.matching { it.name == "build" }.configureEach {
                doLast {
                    delete subproject.layout.buildDirectory
                }
            }
        }
    }
}
